declare @OUTPUTTABLENAME nvarchar(1024) = 'EXPORT_a2c1c4bd_4873_4287_9f47_98b5743d99a2';
declare @BUSINESSPROCESSTATUSID uniqueidentifier = replace(right(@OUTPUTTABLENAME, 36), '_', '-');
declare @PARAMETERTABLENAME nvarchar(100);

declare @BUSINESSPROCESSNAME nvarchar(255);
declare @BUSINESSPROCESSDESCRIPTION nvarchar(1000);
declare @PARAMETERSETID uniqueidentifier;
declare @QUEUEPROCESSNAME nvarchar(255);
declare @QUEUEPROCESSDESCRIPTION nvarchar(1000);

select
	@PARAMETERTABLENAME = BUSINESSPROCESSCATALOG.PARAMETERTABLENAME
from
	dbo.BUSINESSPROCESSSTATUS
	inner join dbo.BUSINESSPROCESSCATALOG on BUSINESSPROCESSCATALOG.ID = BUSINESSPROCESSSTATUS.BUSINESSPROCESSCATALOGID
where
	BUSINESSPROCESSSTATUS.ID = @BUSINESSPROCESSTATUSID

declare @SQL nvarchar(max) = '
	select 
		@BUSINESSPROCESSNAME = [' + @PARAMETERTABLENAME + '].NAME,
		@BUSINESSPROCESSDESCRIPTION = [' + @PARAMETERTABLENAME + '].DESCRIPTION,
		@PARAMETERSETID = [' + @PARAMETERTABLENAME + '].ID
	from 
		dbo.[' + @PARAMETERTABLENAME + '] 
		inner join dbo.BUSINESSPROCESSSTATUS on BUSINESSPROCESSSTATUS.BUSINESSPROCESSPARAMETERSETID = [' + @PARAMETERTABLENAME + '].ID
	where
		BUSINESSPROCESSSTATUS.ID = @BUSINESSPROCESSSTATUSID';

exec sp_executesql @SQL, 
	N'@BUSINESSPROCESSSTATUSID uniqueidentifier, @BUSINESSPROCESSNAME nvarchar(255) output, @BUSINESSPROCESSDESCRIPTION nvarchar(1000) output, @PARAMETERSETID uniqueidentifier output', 
	@BUSINESSPROCESSSTATUSID = @BUSINESSPROCESSTATUSID,
	@BUSINESSPROCESSNAME = @BUSINESSPROCESSNAME output,
	@BUSINESSPROCESSDESCRIPTION = @BUSINESSPROCESSDESCRIPTION output,
	@PARAMETERSETID = @PARAMETERSETID output;

select 
	@QUEUEPROCESSNAME = QUEUEPROCESS.NAME,
	@QUEUEPROCESSDESCRIPTION = QUEUEPROCESS.DESCRIPTION
from 
	dbo.QUEUEPROCESS
	cross apply QUEUEPROCESS.STEPLISTXML.nodes('ArrayOfQueueProcessStep/QueueProcessStep') as T(c)
where
	T.c.value('@ParameterSetID', 'uniqueidentifier') = @PARAMETERSETID

select
	@BUSINESSPROCESSNAME BUSINESSPROCESSNAME,
	@BUSINESSPROCESSDESCRIPTION BUSINESSPROCESSDESCRIPTION,
	@PARAMETERTABLENAME PROCESSNAME,
	@QUEUEPROCESSNAME QUEUEPROCESSNAME,
	@QUEUEPROCESSDESCRIPTION QUEUEPROCESSDESCRIPTION